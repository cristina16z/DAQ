/******************************************* MAIN *******************************************/
Let vToday = Date('30/06/2009', 'DD/MM/YYYY');



/******************************************* MAPS *******************************************/
//Cambio de moneda
Map_CurrencyRate:
Mapping
LOAD * INLINE [
LocalCurrency, Rate
AUD, 1.0908
CAD, 1.0187
EUR, 0.7399
GBP, 0.6650
USD, 1.0000
];


// ShipMethod : 1 tierra, 2 carguero
Map_ShipMethod:
Mapping
LOAD * INLINE [
ShipMethodID, ShipMethodName
1, Por tierra
2, Por carguero
];


//Días de retraso
Map_DiasRetraso:
Mapping LOAD * INLINE [
Dias, Label
1, '1 día de retraso'
2, '2 días de retraso'
3, '3 días de retraso'
];

Map_DiasOrden:
Mapping LOAD * INLINE [
Dias, Orden
1, 2
2, 3
3, 4
];


/******************************************* CUSTOMER *******************************************/
Customer:
LOAD
//    AccountNumber														as CustomerAccountNumber,
    CustomerID															as CustomerID,
    PersonID															as PersonID,
    StoreID																as StoreID,
    TerritoryID															as CustomerTerritoryID

FROM [lib://QVDs/Customer.qvd](qvd)
Where CustomerID>0;



//Person:
Left Join(Customer)
LOAD
    BusinessEntityID													as PersonID,
    FirstName & ' ' & MiddleName & ' ' & LastName						as PersonName	
//  EmailPromotion														as EmailPromotion,														
//  NameStyle															as StylePersonName,
//  PersonType															as PersonType,
//  Title																as TitlePerson,
//  Suffix																as Suffix
FROM [lib://QVDs/Person.qvd](qvd);



//Store:
Left Join(Customer)
LOAD
//    SalesPersonID														as SalesPersonID,
    StoreID,
    StoreName															as StoreName
FROM [lib://QVDs/Store.qvd](qvd);








/******************************************* EMPLOYEE *******************************************/
Employee:
LOAD
    EmployeeBusinessEntityID												as SalesPersonID,
    FirstName & MiddleName & LastName										as EmployeeName,
    CurrentFlag																as CurrentFlag,
    DepartmentName															as DepartmentName,
    Title																	as Title,
    ParentEmployeeBusinessEntityID											as ParentSalesPersonID,
    SalariedFlag															as SalariedFlag,
    SalesPersonFlag															as SalesPersonFlag,
    SalesTerritoryKey														as SalesTerritoryKey
//    BaseRate																as BaseRate,
//    BirthDate																as BirthDate,
//    EmailAddress															as EmailAddress,
//    EmergencyContactName													as EmergencyContactName,
//    EmergencyContactPhone													as EmergencyContactPhone,
//    EndDate																as EmployeeEndDate,
//    Gender																as Gender,
//    HireDate																as HireDate,
//    MaritalStatus															as MaritalStatus,
//    PayFrequency															as PayFrequency,
//    Phone																	as Phone,
//    SickLeaveHours														as SickLeaveHours,
//    StartDate																as EmployeeStartDate,
//    Status																as EmployeeStatus,
//    VacationHours															as VacationHours
FROM [lib://QVDs/Employee.qvd](qvd)
WHERE EmployeeBusinessEntityID > 273; 										// personas que están dedicadas a ventas > 273









/******************************************* PRODUCT *******************************************/

//Product Model < Product

Map_ProductModel:
Mapping
LOAD
	ProductModelID															as ProductModelID,
    Name 																	as ProductModelName
FROM [lib://QVDs/ProductModel.qvd](qvd);


Product:
LOAD
    ProductID																as ProductID,
    Name 																	as ProductName,
    ProductNumber															as ProductNumber,
    ProductModelID															as ProductModelID,
    ApplyMap('Map_ProductModel', ProductModelID) 							as ProductModelName,
    ProductSubcategoryID													as ProductSubcategoryID,
    StandardCost															as StandardCost,
    ListPrice																as ListPrice,
    MakeFlag																as MakeFlag,
    ProductLine																as ProductLine
//    "Class"																as ProductClass,
//    "Color"																as ProductColor,
//    DaysToManufacture														as DaysToManufacture,
//    DiscontinuedDate														as DiscontinuedDate,
//    FinishedGoodsFlag														as FinishedGoodsFlag,
//    ReorderPoint															as ReorderPoint,
//    SafetyStockLevel														as SafetyStockLevel,
//    SellEndDate															as SellEndDate,
//    SellStartDate															as SellStartDate,
//    Size																	as Size,
//    SizeUnitMeasureCode													as SizeUnitMeasureCode,
//    Style																	as ProductStyle,
//    Weight																as ProductWeight,
//    WeightUnitMeasureCode													as WeightUnitMeasureCode
FROM [lib://QVDs/Product.qvd](qvd);
//WHERE Exists(ProductID);



//ProductCategory < Product SubCategory < Product
Map_ProductCategory:
Mapping
LOAD
	ProductCategoryID														as ProductCategoryID,
    Name																	as ProductCategoryName 
    
FROM [lib://QVDs/ProductCategory.qvd](qvd);


//ProductSubcategory:
Left Join(Product)
LOAD
	ProductSubcategoryID 													as ProductSubcategoryID,
    Name 																	as ProductSubcategoryName,
    ApplyMap('Map_ProductCategory', ProductCategoryID)						as ProductCategoryName
FROM [lib://QVDs/ProductSubcategory.qvd](qvd);




/******************************************* VENDOR *******************************************/
// Vendor < Product Vendor < Product

ProductVendor:
LOAD
    BusinessEntityID 														    as VendorID,
    AverageLeadTime															    as AverageLeadTime,
    ProductID																    as ProductID,
    StandardPrice															    as StandardPrice
//    LastReceiptCost															as LastReceiptCost,
//    LastReceiptDate															as LastReceiptDate,
//    MaxOrderQty																as MaxOrderQty,
//    MinOrderQty																as MinOrderQty,
//    OnOrderQty																as OnOrderQty,
//    UnitMeasureCode															as UnitMeasureCode
FROM [lib://QVDs/ProductVendor.qvd](qvd);





//Vendor:
Left Join(ProductVendor)
LOAD
	BusinessEntityID														as VendorID,
    Name																	as VendorName,					
    PreferredVendorStatus													as PreferredVendorStatus,
    ActiveFlag																as ActiveFlag										
//   CreditRating															as CreditRating,
//    AccountNumber															as VendorAccountNumber							

FROM [lib://QVDs/Vendor.qvd](qvd)
WHERE Len(Trim(BusinessEntityID))>0 and ActiveFlag<>0;						//sólo los proveedores activos/fav





Left Join(Product)
LOAD * 
Resident ProductVendor;
Drop Table ProductVendor;




/******************************************* SALES *******************************************/

SalesOrderHeader:
LOAD
	SalesOrderID																as SalesOrderID,
    CustomerID																	as CustomerID,
    TerritoryID																	as SalesTerritoryID,
    OrderDate																	as OrderDate,
	AccountNumber																as SalesAccountNumber,
    BillToAddressID																as BillToAddressID,
    ShipToAddressID																as ShipToAddressID,
    Freight																		as Freight,
    TaxAmt																		as TaxAmt,
    LocalCurrency																as LocalCurrency,
    OnlineOrderFlag																as OnlineOrderFlag,
    PurchaseOrderNumber															as PurchaseOrderNumber,
//    RevisionNumber															as RevisionNumber,
//    SalesOrderNumber															as SalesOrderNumber,
    ShipMethodID																as ShipMethodID,
    Status																		as StatusID 
FROM [lib://QVDs/SalesOrderHeader.qvd](qvd);



SalesOrderDetail:																//Load Precedente
LOAD
    *,
    If(DiasRetraso <= 0, 1, 0) as EsATiempo,									//FlagATiempo= 1 si es antes o puntuala la fecha,sino 0
    If(DiasRetraso > 0, 1, 0) as EsRetrasado,									//FlagRetrasado = 1 si llega con retraso
	
    Dual(																		//Dual mete número interno para que se ordene (txt,num)
        If(DiasRetraso<=0, 'A Tiempo',														
        If(DiasRetraso>=4, '> 3 días de retraso',								//si está entre 0 - 3,escribe los días de retraso
           Pick(Match(DiasRetraso,1,2,3),										//busca valor diasRetraso dentro de la lista 1,2,3, sino 0
                '1 día de retraso','2 días de retraso','3 días de retraso')		//Num pick para escoger txt - match te devuelve numposición
           )
        ),																		//Resultado Final Orden
        If(DiasRetraso<=0, 1,													// <=0 A tiempo - num 1 orden
           If(DiasRetraso>=4, 5, DiasRetraso+1))								//>=4 díasRetraso - num 5 orden
    ) as TiempoDeEnvio															// DiasRetraso+1 - num orden diasRetraso+1
;

LOAD
	SalesOrderID																as SalesOrderID,
    SpecialOfferID																as SpecialOfferID,
    SalesOrderDetailID															as SalesOrderDetailID,
    ProductID																	as ProductID,
    OrderQty																	as OrderQty,
    UnitPrice																	as UnitPrice,
    UnitPriceDiscount															as UnitPriceDiscount,
//    CarrierTrackingNumber														as CarrierTrackingNumber,
    DueDate																		as DueDate,
    ShipDate																	as ShipDate,
    Floor(ShipDate) - Floor(DueDate) 											as DiasRetraso
FROM [lib://QVDs/SalesOrderDetail.qvd](qvd);



SpecialOffer:
Left Join(SalesOrderDetail)
LOAD
    SpecialOfferID																as SpecialOfferID,
    Description																	as SpecialOfferDescription,
    DiscountPct																	as DiscountPct,
    "Type"																		as SpecialOfferType,
    Category																	as SpecialOfferCategory
FROM [lib://QVDs/SpecialOffer.qvd](qvd);


Left Join(SalesOrderHeader)
LOAD * 
Resident SalesOrderDetail;
Drop Table SalesOrderDetail;



//SalesTerritory_Sales:
Left Join(SalesOrderHeader)
LOAD
	TerritoryID																	as SalesTerritoryID,
    CountryRegionCode															as SalesTerritoryCountryRegionCode,
    "Group"																		as SalesTerritoryGroup,
    Name 																		as SalesTerritorySalesName
FROM [lib://QVDs/SalesTerritory.qvd](qvd);


//SalesTerritory_Customer:
Left Join(Customer)
LOAD
	TerritoryID																	as CustomerTerritoryID,
    CountryRegionCode															as CustomerTerritoryCountryRegionCode,
    "Group"																		as CustomerTerritoryGroup,
    Name 																		as CustomerTerritorySalesName    
FROM [lib://QVDs/SalesTerritory.qvd](qvd);


SalesTerritoryHistory:
LOAD
    BusinessEntityID                               								as SalesPersonID,     
    TerritoryID                                    								as SalesTerritoryID,  
    StartDate                         											as TerritoryStartDate,
    Date(If(IsNull(EndDate), Today(), EndDate))									as TerritoryEndDate
FROM [lib://QVDs/SalesTerritoryHistory.qvd](qvd);




/******************************************* ADDRESS *******************************************/

/*
Concatenate (Address)
Address:
LOAD
    AddressID															as AddressID,
    AddressLine1														as AddressLine1,
    If(Len(Trim(AddressLine2))=0, '', AddressLine2)						as AddressLine2,
    City																as AddressCity,
    CountryRegionCode													as AddressCountryRegionCode,
    CountryRegionName													as AddressCountryRegionName,
//    IsOnlyStateProvinceFlag												as AddressIsOnlyStateProvinceFlag,
    PostalCode															as AddressPostalCode,
    StateProvinceCode													as AddressStateProvinceCode,
    StateProvinceName													as AddressStateProvinceName,
    TerritoryID															as AdressTerritoryID,
    'Ship'                                   							as typeAddress
FROM [lib://QVDs/Address.qvd](qvd)
WHERE Exists(ShipToAddressID, AddressID);*/




Address_BillTo:
LOAD
    AddressID                            as BillToAddressID,
    AddressLine1                         as BillToAddressLine1,
    AddressLine2                         as BillToAddressLine2,
    City                                 as BillToCity,
    CountryRegionCode                    as BillToCountryRegionCode,
    CountryRegionName                    as BillToCountryRegionName,
//    IsOnlyStateProvinceFlag              as BillToIsOnlyStateProvinceFlag,
    PostalCode                           as BillToPostalCode,
    StateProvinceCode                    as BillToStateProvinceCode,
    StateProvinceName                    as BillToStateProvinceName,
    TerritoryID                          as BillToAddressTerritoryID
FROM [lib://QVDs/Address.qvd](qvd);

Address_ShipTo:
LOAD
    AddressID                            as ShipToAddressID,
    AddressLine1                         as ShipToAddressLine1,
    AddressLine2                         as ShipToAddressLine2,
    City                                 as ShipToCity,
    CountryRegionCode                    as ShipToCountryRegionCode,
    CountryRegionName                    as ShipToCountryRegionName,
//    IsOnlyStateProvinceFlag              as ShipToIsOnlyStateProvinceFlag,
    PostalCode                           as ShipToPostalCode,
    StateProvinceCode                    as ShipToStateProvinceCode,
    StateProvinceName                    as ShipToStateProvinceName,
    TerritoryID                          as ShipToAddressTerritoryID
FROM [lib://QVDs/Address.qvd](qvd);


/******************************************* QUOTA *******************************************/
QuotaTemp:
CROSSTABLE (EmployeeID, QuotaAmount, 1)
LOAD
    Date(F1,'DD/MM/YYYY')																	as QuotaFecha,
    "275"																					as 275,
    "276"																					as 276,
    "277"																					as 277,
    "279"																					as 279,
    "280"																					as 280,
    "282"																					as 282,
    "284"																					as 284,
    "286"																					as 286,
    Website																					as 285,
    "288"																					as 288,
   	"289"																					as 289,
   	"290"																					as 290
FROM [lib://QVDs/Quota.qvd](qvd);


Quota:
Load
	text(EmployeeID)																		as SalesPersonID,
    QuotaFecha as QuotaFecha,
    QuotaAmount as QuotaAmount
Resident QuotaTemp;



SalesTerritoryHistoryQuota:
LOAD
    text(BusinessEntityID)                               						as SalesPersonID,     
    TerritoryID                                    								as SalesTerritoryID,  
    StartDate                         											as TerritoryStart,
    Date(If(IsNull(EndDate),'$(vToday)', EndDate))								as TerritoryEnd
FROM [lib://QVDs/SalesTerritoryHistory.qvd](qvd);
Drop Table QuotaTemp;


Left Join (SalesTerritoryHistoryQuota)
IntervalMatch(QuotaFecha)
Load distinct
TerritoryStart, TerritoryEnd
Resident SalesTerritoryHistoryQuota;


Left Join (Quota)
Load *
Resident SalesTerritoryHistoryQuota;

drop table SalesTerritoryHistoryQuota;
Drop table SalesTerritoryHistory;












/******************************************* MASTERCALENDAR *******************************************/
Let vToday = Date('30/06/2009', 'DD/MM/YYYY');

// Generate MinMaxTemp table
[MinMaxTemp]:
LOAD
	DATE(MIN(FieldValue('OrderDate', RecNo()))) AS MinDate,
    DATE(MAX(FieldValue('OrderDate', RecNo()))) AS MaxDate
AUTOGENERATE FieldValueCount('OrderDate');
 
// Create variable MaxDate
//LET vToday = NUM(PEEK('MaxDate', 0, 'MinMaxTemp'));
 

[MasterCalendar_Temp]:
LOAD
	DATE(MinDate + IterNo() - 1) AS OrderDate
Resident MinMaxTemp
WHILE MinDate + IterNo() - 1 <= MaxDate;
DROP TABLE MinMaxTemp;
 

 
// MasterCalendar 
[MasterCalendar]:
LOAD
	OrderDate,
    Day(OrderDate) AS Day,
    
    //Año natural
   	Year(OrderDate) AS CalendarYear,
    
    //Periodo Fiscal
    Mod(Month(OrderDate)+5,12)+1 AS FiscalPeriod,
    //Primer mes Julio
    Dual(Month(OrderDate), Mod(Month(OrderDate)+5,12)+1) AS Month,
    //Mes-Año
 	Date(MonthStart(OrderDate), 'MMM-YYYY') AS MonthYear,
    
  	//trimestre natual
//  	Dual('Q' & Ceil(Month(OrderDate)/3), Ceil(Month(OrderDate)/3)) AS CalendarQuarter,
    
	//trimestre fiscal
    // 	Dual('Q' & Ceil(Month(OrderDate) / 3), Ceil(Month(OrderDate) / 3)) AS Quarter,
    Dual('Q' & Ceil((Mod(Month(OrderDate)+5,12)+1)/3),Ceil((Mod(Month(OrderDate)+5,12)+1)/3)) AS Quarter,

    
    //Año fiscal julio > junio
    If(Month(OrderDate)>=7, Year(OrderDate)+1, Year(OrderDate)) AS FiscalYear,
    
    //Comprar 2ndo meses entre trimestres por ej
	Mod(Mod(Month(OrderDate)+5,12),3)+1 AS FiscalMonthInQuarter,


        																// 1 = dentro del rango y 0 = fuera del rango
    InYearToDate(OrderDate,$(vToday), 0,7)* -1 AS FYTDFlag,				//FiscalYearToDate. 1 = inicio año  fiscal hasta hoy
    InYearToDate(OrderDate,$(vToday), -1, 7)* -1 AS FLYTDFlag,			//FiscalLastYearToDate  1 = un año fiscal atrás
    InQuarterToDate(OrderDate, $(vToday), 0, 7) * -1 AS FQTDFlag,		//FiscalQuarterToDate 1 = desde el inicio del trimestre hasta ahora
	InMonthToDate(OrderDate, $(vToday), 0)     * -1 AS FMTDFlag		//FiscalMonthToDate 1 = desde inicio del mes hasta ahora
    
//     If(DayNumberOfYear(OrderDate) <= DayNumberOfYear($(vToday)), 1, 0) AS IsInYTD,
//     If(DayNumberOfQuarter(OrderDate) <= DayNumberOfQuarter($(vToday)), 1, 0) AS IsInQTD,
//     If(Day(OrderDate) <= Day($(vToday)), 1, 0) AS IsInMTD,
//     If(Month(OrderDate) = Month($(vToday)), 1, 0) AS IsCurrentMonth,
//     If(Month(AddMonths(OrderDate, 1)) = Month($(vToday)), 1, 0) AS IsLastMonth
    
    
RESIDENT MasterCalendar_Temp;
DROP TABLE MasterCalendar_Temp;